<CsoundSynthesizer>
<CsOptions>
</CsOptions>
<CsInstruments>

;matthew j thies 5/4/00

sr 		= 		44100
kr 		= 		4410
ksmps 	= 		10
nchnls 	= 		2

gacmb	init	0
garvb	init	0


;PLUCK EXPLOSIONS W/ REVERB 1 SETTING

;==============================================================================================================
;==============================================================================================================


		    instr   110			                           ;pluck explosions		

;==============================================================================================================
kamp		=		ampdb (p4)
kcps		=		p5
icps		=		p6
ifn			=		p7
imeth		=		p8
iparm		=		p9
;==============================================================================================================
iamp		=		1										;amplitude env, linen
iatk		=		p10
idur		=		p3
idec		=		p11
;==============================================================================================================
kfco		=		p12										;resonant filter, rezzy
kres		=		p13
;==============================================================================================================
ipval1		=		p14  									;pitch env, linseg
ipdur1		=		p15
ipval2		=		p16								
;==============================================================================================================
ipanfn		=		p17										;pan parameters
kpanfrq		=		1/p3
;==============================================================================================================
irvbsnd	    =       p18
;==============================================================================================================
ifc1		=		p12
ifcdur1		=		p3
ifc2		=		p19
;==============================================================================================================

kfcoenv		linseg  ifc1, ifcdur1, ifc2
kpan		oscil	1, kpanfrq, ipanfn            
kcpsenv		linseg	ipval1, ipdur1, ipval2
kampenv		linen	iamp, iatk, idur, idec
asig 		pluck 	kamp, kcpsenv, icps, ifn, imeth, iparm
arez		rezzy	asig, kfcoenv, kres	
a1          =       arez * kampenv                         
                                 
            outs    a1 * kpan, a1 * (1 - kpan)

garvb	    =	    garvb+(a1*irvbsnd)

            endin


;==============================================================================================================
;==============================================================================================================

 
		    instr   111			                           ;pluck shaker		

;==============================================================================================================
kamp		=		ampdb (p4)
kcps		=		p5
icps		=		p6
ifn			=		p7
imeth		=		p8
iparm		=		p9
;==============================================================================================================
iamp		=		1										;amplitude env, oscil
idur		=		1/p3*60
ienvfn		=		p10
;==============================================================================================================
kfco		=		p11										;resonant filter, rezzy
kres		=		p12
;==============================================================================================================
ipval1		=		p13  									;pitch env, linseg
ipdur1		=		p14
ipval2		=		p15								
;==============================================================================================================
ipanfn		=		p16										;pan parameters
kpanfrq		=		1/p3
;==============================================================================================================
irvbsnd	    =       p17
;==============================================================================================================
ifc1		=		p11
ifcdur1		=		p3
ifc2		=		p18
;==============================================================================================================

kfcoenv		linseg  ifc1, ifcdur1, ifc2
kpan		oscil	1, kpanfrq, ipanfn            
kcpsenv		linseg	ipval1, ipdur1, ipval2
kampenv		oscil	iamp, idur, ienvfn
asig 		pluck 	kamp, kcpsenv, icps, ifn, imeth, iparm
arez		rezzy	asig, kfcoenv, kres	
a1          =       arez * kampenv                         
                                 
            outs    a1 * kpan, a1 * (1 - kpan)

garvb	    =	    garvb+(a1*irvbsnd)

            endin


;==============================================================================================================
;==============================================================================================================

 
				instr 	199                     ;global reverb
				
;==============================================================================================================
idur	   =	p3					
irvbtim	   =	p4
ihiatn	   =	p5
;==============================================================================================================
krvbtim	   linseg	5, 44, 8	
khiatn	   linseg	0, p3*.5, 1, p3*.5, 0
arvb	   nreverb	garvb, krvbtim, ihiatn
		   outs		arvb, arvb
garvb	   =		0
		   endin




</CsInstruments>
;--------------------------------------------------------------------------------------------------------------------------------------------------
;--------------------------------------------------------------------------------------------------------------------------------------------------
<CsScore>

;Function 1 uses the GEN10 subroutine to compute a sine wave
;Function 2 uses the GEN10 subroutine to compute the first sixteen partials of a sawtooth wave
;Function 3 uses the GEN20 subroutine to compute a Hanning window for use as a grain envelope
;Functions 4 use the GEN07 subroutine to compute a linear pan function R-L
;Functions 5 use the GEN07 subroutine to compute a linear pan function L-R
;Functions 6 use the GEN05 subroutine to compute a exponential pan function R-L
;Functions 7 use the GEN05 subroutine to compute a exponential pan function L-R
;Functions 12 use the GEN05 subroutine to compute a exponential pan function L-R-L
;Functions 13 use the GEN05 subroutine to compute a exponential pan function R-L-R
;Functions 8, 9 use the GEN7 subroutine to draw a high sustain AR envelope 
;Functions 10, 11 use the GEN7 subroutine to draw a low sustain AR envelope 

f1   0 4096  10   1    
f2   0 4096  10   1  .5 .333 .25 .2 .166 .142 .125 .111 .1 .09 .083 .076 .071 .066 .062
f3   0 4097  20   2  1
f4	 0 4096   7   0  4096  1
f5	 0 4096   7   1  4096  0
f6	 0 4096   5   .001  4096  1
f7 	 0 4096   5   1  4096  .001
f8   0 4096   7   0  256  .37  512  .68  512  .87  384  .94  384  .97  384  .94  384  .87  512  .68  512  .37  256  0
f9   0 4096   7   0  256  .18  512  .34  512  .43  384  .47  384  .49  384  .47  384  .43  512  .34  512  .18  256  0
f10  0 4096   7   .97  256  .94  512  .87  512  .68  384  .37  384  0  384  .37  384  .68  512  .87  512  .94  256  .97
f11  0 4096   7   .49  256  .47  512  .43  512  .34  384  .18  384  0  384  .18  384  .34  512  .43  512  .47  256  .49
f12	 0 4096   5   1  2048  .001  2048  1
f13	 0 4096   5   .001  2048  1  2048  .001

;Adv statement
;           time
;a   0   0   44


;Tempo statements
;  t1  tempo1  t2  tempo2  t3  tempo3
t  0   60      20  80      32  40

;==================================================================================================================================================================================================================
;==================================================================================================================================================================================================================


;SHAKERS
;p1		   p2      p3        p4     p5      p6    p7   p8    p9     p10    p11     p12     p13      p14 	  p15      p16		p17     p18   
;inst	   strt    dur       amp    frq     ifrq  fn   meth  parm1	envfn  fco     res	   stfrq    frqdur    endfrq   panfn    rvbsnd  fc2
;==================================================================================================================================================================================================================
i111       21      5		 60 	5       10    0    3     .5      13	   10000   50	   5        5         5        4       .1       5000
i111       22      .		 <  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       23      .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       23.5    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       24      .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       24.3    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       24.6    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       24.9    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       25      .		 80  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
;==================================================================================================================================================================================================================
i111       29      .		 60 	5       10    0    3     .5      13	   10000   75	   5        5         5        4       .        5000
i111       30      .		 <  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       31      .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       31.5    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       32      .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       32.3    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       32.6    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .
i111       32.9    .		 .  	5       10    0    3     .5      13	   10000   .	   5        5         5        5       .        .
i111       33      .		 80  	5       10    0    3     .5      13	   10000   .	   5        5         5        4       .        .



;PLUCK EXPLOSIONS
;p1		   p2       p3       p4     p5      p6    p7   p8    p9     p10    p11   p12       p13      p14 	  p15      p16		p17     p18		p19
;inst	   strt     dur      amp    frq     ifrq  fn   meth  parm1	atk	   dk	 fco       res		stfrq     frqdur   endfrq	panfn   rvbsnd  fc2
;==================================================================================================================================================================================================================
i110        0       5		 80 	5       110   0    3     1       2     1	 10000      1		5         5        5        9       .1      10000
i110        4       5		 .		5       <     0    3     1	     1     1	 <         <		5         5        5        8       <       <
i110        8       7	     . 		5       100    0    3     1	    .5     1     .         .		5         5        5        10      .       .
i110       10       5	     70 	5       330   0    3     1	     1      .5   .         .		5         5        5        11      .       .
;==================================================================================================================================================================================================================
i110       11       5		 80		5       90    0    3     0	    .5     2.5	 .         3		5         5        5        11      .       15000
i110       14       5		 70  	5       10    0    3     <	    .5     2.5	 .         5		5         5        5        12      .       500
i110       16      11		 80		5       80    0    3     .	    .1     2	 .         3.5	    6         5        5        4       .       10000
i110       18       5		 70		5       9     0    3     .	    .1     2	 .         5	    6         5        5        13      .       100
i110       20       7		 80		5       70    0    3     .	    .1     1	 .         4	    6         5        5        5       .7      10000
;==================================================================================================================================================================================================================
i110       24       5		 55 	5       60    0    3     .	    .1     1	 .         <        6         5        5        6       .9      300
i110       24.5     5		 65 	5       110   0    3     1       2     1	 10000      1		5         5        5        6       .1      10000
i110       24.5     5		 70 	5       .     0    3     1       2     1	 10000      1		5         5        5        7       .1      10000
;==================================================================================================================================================================================================================
i110       28       5		 60		5       50    0    3     .	    .1     1	 .         .        6         5        5        7       .9      200 
i110       28.5     5		 70 	5       220   0    3     1       2     1	 10000      1		5         5        5        7       .1      10000
i110       28.5     5		 75 	5       .     0    3     1       2     1	 10000      1		5         5        5        6       .1      10000
;==================================================================================================================================================================================================================
i110       32       5		 65		5       40    0    3     .5	    .1     1  	 .         .        7         5        5        6       .9      100
i110       32.5     5		 75 	5       330   0    3     1       2     1	 10000      1		5         5        5        6       .1      10000
i110       32.5     5		 80 	5       .     0    3     1       2     1	 10000      1		5         5        5        7       .1      10000
;==================================================================================================================================================================================================================
i110       34       5		 75		5       30    0    6     0	    .5     1.5	 .         .        5.5       5        5        7       .8      .
i110       38       5		 70		5       <     0    6     0	    .9      .1	 .         .        5.5       5        5        6       .85     .
i110       42       5		 75		5       10    0    6     0	    .1     1.9	 5000      10       5.5       5        5        7       .9      5000
i110       42       .		 .		.       .     .    .     .	    .      .	 .         .        .         .        .        6       .       .




;GLOBAL REVERB
;p1        p2       p3       p4     p5    
;          Sta      Dur      rvbtim hiatn    
;==================================================================================================================================================================================================================
i199       0        52       5      0

e



</CsScore>
</CsoundSynthesizer>


